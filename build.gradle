import com.github.spotbugs.SpotBugsTask

plugins {
	id "java-library"
	id "maven-publish"
	id "checkstyle"
	id "pmd"
	id "com.github.spotbugs" version "3.0.0"
	id "jacoco"
}

group = "com.therandomlabs.curseapi"
version = "1.0.0"

repositories {
	mavenCentral()
}

dependencies {
	implementation "com.google.guava:guava:28.1-jre"
	implementation "org.slf4j:slf4j-api:2.0.0-alpha1"
	implementation "com.squareup.moshi:moshi:1.9.2"
	implementation "com.squareup.retrofit2:retrofit:2.6.2"
	implementation "com.squareup.retrofit2:converter-moshi:2.6.2"
	implementation "org.jsoup:jsoup:1.12.1"

	testRuntimeOnly "org.apache.logging.log4j:log4j-core:2.12.1"
	testRuntimeOnly "org.apache.logging.log4j:log4j-slf4j18-impl:2.12.1"

	testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0-M1"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0-M1"
	testImplementation "org.assertj:assertj-core:3.14.0"

	spotbugsPlugins "com.mebigfatguy.fb-contrib:fb-contrib:7.4.3.sb"
	spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.1"
	spotbugsPlugins "jp.skypencil.findbugs.slf4j:bug-pattern:1.5.0@jar"
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = "CLASS"
			//We only test the ForgeSVC package since most other classes are either
			//abstract classes, interfaces or small utility classes.
			includes = [
			        "com.therandomlabs.utils.curseapi.forgesvc.*"
			]

			limit {
				counter = "INSTRUCTION"
				value = "COVEREDRATIO"
				minimum = 0.8
			}
		}
	}
}

check.dependsOn jacocoTestCoverageVerification

pmd {
	ruleSets = []
	ruleSetConfig = resources.text.fromFile("config/pmd.xml")
}

spotbugs {
	excludeFilter = file("config/spotbugs-exclude.xml")
}

tasks.withType(SpotBugsTask) {
	reports {
		xml.enabled false
		html.enabled true
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	archiveClassifier.set("sources")
	from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
	archiveClassifier.set("javadoc")
	from javadoc
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/TheRandomLabs/CurseAPI")

			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
				password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
			}
		}
	}

	publications {
		gpr(MavenPublication) {
			artifact sourcesJar
			artifact javadocJar
			from components.java
		}
	}
}
